// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Increment
    Inc16(in=feedback, out=i);

    // Inc or out[t]
    Mux16(a=feedback, b=i, sel=inc, out=io);
    // Load or incmux
    Mux16(a=io, b=in, sel=load, out=lio);
    // Reset or incloadmux
    Mux16(a=lio, b=false, sel=reset, out=rlio);

    // Store in register
    Register(in=rlio, load=true, out=feedback, out=out);
}
